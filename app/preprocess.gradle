import groovy.io.FileType

def resRaw = 'src/main/res/raw'

task preprocess {
    doLast {
        def moduleDir = project.file("${project.projectDir}/../modules")
        if (moduleDir.isDirectory()) {
            moduleDir.eachFileRecurse(FileType.DIRECTORIES) { file ->
                println "generate resources for ${file.name}"
                def (modcode, modname) = file.name.tokenize('-')

                ant.exec(executable: '../util/libmake', failonerror: true) {
                    arg value: '--rom'
                    arg value: file.canonicalPath
                    arg value: resRaw
                    arg value: modcode
                }
            }
        }

        def programDir = project.file("${project.projectDir}/../programs")
        if (programDir.isDirectory()) {
            programDir.eachFileRecurse(FileType.DIRECTORIES) { file ->
                println "generate resources for ${file.name}"

                def commonArgs = [
                    "${resRaw}/${file.name}.ti5p",
                    "help00=${file.canonicalPath}/help",
                    "cardsrc00=${file.canonicalPath}/card",
                    "progsrc00=${file.canonicalPath}/prog"
                ]

                def extraArgs = []
                if (file.name == "demo_card_store") {
                    extraArgs = [
                        "banksrc9004=${file.canonicalPath}/bank900",
                        "banksrc9014=${file.canonicalPath}/bank901",
                        "banksrc9022=${file.canonicalPath}/bank902",
                        "banksrc9052=${file.canonicalPath}/bank905",
                        "mem9033=${file.canonicalPath}/mem903",
                        "mem9043=${file.canonicalPath}/mem904",
                        "mem9052=${file.canonicalPath}/mem905"
                    ]
                }

                ant.exec(executable: '../util/makeprog', failonerror: true) {
                    (commonArgs + extraArgs).each { arg value: it }
                }
            }
        }
    }
}

task customClean {
    doLast {
        def rawDir = project.file("${project.projectDir}/$resRaw")
        if (rawDir.isDirectory()) {
            ant.delete(includeEmptyDirs: false) {
                fileset(dir: rawDir) {
                    include(name: '**/*.ti5l')
                    include(name: '**/*.ti5p')
                }
            }
        }
    }
}

clean.dependsOn customClean
preBuild.dependsOn preprocess
